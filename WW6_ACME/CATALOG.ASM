;
!zone CATALOG
;
;
CATLOG:
 JSR CLALL
 LDA #2
 LDX #<CATD
 LDY #>CATD
 JSR SETNAM
 LDA #2
 JSR XDEVICEN
 LDY #0
 JSR OPENE1
 JSR CHNL2I
;
 LDX #4
 BNE DLOOP         ;LINE LINK
OLOOP:
 JSR GETX     ;DISK NAME
 LDX #$02
DLOOP:
 JSR PULLX         ;LINE LINK
 JSR SIZE          ;BLOCKSIZE
 JMP OLOOP         ;FNAME
;
GETX:
 JSR CHNL2I
 JSR CHRIN
 LDX $90           ;STATUS
 BNE CRALF
 PHA
 JSR CLRCHN
 PLA
 BEQ CRALF         ;CR/LF STOP KEY
;
 JSR CHROUT
 JMP GETX
;
ABORT:
 PLA
 PLA
; JSR CLRCHN
;*NOTE* THIS MAY BE COMPLETELY UNNEC, CLOSE2 DOES CLRCHN AFTER...
 JMP CLOSE2
;
CRALF:
;
 LDA CURRENT_KEY
 CMP #$BE               ;COMM-V?
 BNE .CRLF
;
 LDY #26                ;FILE-TYPE POSITION (LETTER)
 LDA ($D1),Y            ;SCREEN ROW
 CMP #7                 ;SEQ FILE?
 BNE .CRLF
 LDA #0 
 STA $D3
 BEQ .JOIN
;
.CRLF:
 JSR CRLF
;
.JOIN:
 JSR GETIN1
 CMP #3
 BEQ ABORT
 JMP CHNL2I
;
PULLX:
 JSR CHRIN      ;THROW AWAY .X
 DEX            ;DISK CHARACTERS
 BNE PULLX
 RTS
;
SIZE:
 JSR CHNL2I     ;INPUT CHANNEL
 JSR CHRIN      ;PRINT BLOCK SIZE
 STA TEMP0
 JSR CHRIN
 LDY TEMP0
 LDX $90
 BNE ABORT
 JSR COUT
 JSR CLRCHN
 LDY #0         ;ALL POSITIVE,NO SPACE
CALOOP:
 LDA $100,Y
 BEQ CALOO1
 JSR CHROUT
 INY
 BNE CALOOP
CALOO1:
 RTS
;
;
!eof
;
