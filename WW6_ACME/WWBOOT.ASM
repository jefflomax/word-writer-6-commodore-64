;
!zone WWBOOT
;
; WORD WRITER BOOTER
;  BY JEFF LOMAX
;
FAST_LOAD_ADDRESS =  $C000
WORD_WRITER_ADDRESS =  $0801
WORD_WRITER_RUN =  $0827-1
;
CLALL =  $FFE7
CLOSE =  $FFC3
CLRCHN =  $FFCC
SETLFS =  $FFBA
SETNAM =  $FFBD
LOAD =  $FFD5
;
;BASIC START
;
!initmem $FF
 * =  $801
!byte $24,8,$A
;
CBUF2: 
;
!text 0,$9E,"2087",$3A
!byte $8F,34,20,20,20,20,20,20,20
!text 20,20,20,20,20,"WORD WRITER"
!byte 0,0,0
;
; BRANCH TO WORD WRITER
;
BEGIN:
;
 SEI
 CLD
 LDX #$FF
 TXS
;
 LDA $BA
 STA DRIVE	;GET CURRENT DRIVE
 JSR CLRCHN
 JSR CLALL
;
 LDA $02FF
 CMP #$A5
 BEQ NOT_GEOS
;
 LDX #$0
 STX $02FF	;GEOS FLAG
;
 LDX #3
CKGEOS
 LDA $C006,X
 CMP GEOSMG,X
 BNE NOT_GEOS
 DEX
 BPL CKGEOS
 LDA #$A5
 STA $02FF	;FLAG GEOS PRESENT
;
NOT_GEOS:
;
 LDA $330	;GET SYSTEM LOAD VECTOR
 CMP #165	;HAS IT BEEN CHANGED?
 BEQ .LOAD_FL	;BIF SO TO LOAD FL
 LDA $331	;GET SYSTEM LOAD VECTOR HI
 CMP #244	;HAS IT BEEN CHANGED?
 BNE .NOT_THERE	;IF NOT CHANGED, SKIP LOAD
;
.LOAD_FL:
 LDA #2
 LDX #<FAST_LOAD_NAME
 LDY #>FAST_LOAD_NAME
 JSR SETNAM
;
 LDA #2
 LDX DRIVE
 LDY #0
 JSR SETLFS
;
 LDX #<FAST_LOAD_ADDRESS
 LDY #>FAST_LOAD_ADDRESS
 LDA #0
 JSR LOAD
 LDA #2
 JSR CLOSE 
 JSR CLRCHN
;
 LDA $C000
 CMP #$A9	
 BNE .NOT_THERE
 LDA $C020
 CMP #$F5
 BNE .NOT_THERE
 LDA $C040
 CMP #$4C
 BNE .NOT_THERE		;JUST TEST 3 BYTES TO SEE IF FILE LOADED
;
 JSR $C000		;ENABLE SWIFT-LOAD
;
.NOT_THERE:
;
 LDX #0
.LOOP:
 LDA MESSAGE,X
 BEQ .DONE
 JSR $FFD2
 INX
 BNE .LOOP
;
.DONE:
;
 LDA #3
 LDX #<WORD_WRITER_NAME
 LDY #>WORD_WRITER_NAME
 JSR SETNAM
;
 LDA #2
 LDX DRIVE
 LDY #0
 JSR SETLFS
;
 LDX #<WORD_WRITER_ADDRESS
 LDY #>WORD_WRITER_ADDRESS
 LDA #>WORD_WRITER_RUN
 PHA
 LDA #<WORD_WRITER_RUN
 PHA			;SET WORD WRITER RUN ADDRESS
 LDA #0			;LOAD
 JMP LOAD
;
MESSAGE:
!text 13,"LOADING WORD WRITER 4",0
DRIVE:	
!byte 0
FAST_LOAD_NAME:
!text "FL"
WORD_WRITER_NAME:
!text "WW4"
GEOSMG: 
!text "GEOS BOOT"
;
;
!text " DEDICATED TO MARK GOLDBERG, VICTOR SC"
!text "HILLER, NICHOLAS ALEXANDER, & TIMEWORKS."
;
;
;
!eof
;
