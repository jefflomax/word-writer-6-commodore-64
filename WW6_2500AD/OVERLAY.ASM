;
 TITLE OVERLAY
 PAGE
 PW 92
;
 INCLUDE WWSETUP.ASM
 INCLUDE VARIABLS.ASM
;
; PUBLIC IN_DATABASE_MODE
 PUBLIC FIELD_NAMES
; PUBLIC FC_MINI_DATABASE
 PUBLIC FC_ENTER_OUTLINER
 PUBLIC LOAD_OVERLAY_DIRECT		
 PUBLIC OUTMODE
 PUBLIC OVERLAY_IN_MEMORY		
 PUBLIC OVERLAY_NUMBER
;
 EXTERN DRAWOUT				;PART2
 EXTERN PRKECY				;DATA
 EXTERN ENTER_DATABASE_MODE		;MINIDB
 EXTERN EXIT_DATABASE_MODE		;MINIDB
 EXTERN INSERTM				;CONFIG
 EXTERN LOAD_ANY_FILE			;SUPPORT (MONSTER)
 EXTERN MNLINE				;PART1
 EXTERN	ON_AND_REFREASH			;EDITLN
 EXTERN OVERLAY_START			;MINIDB & OUTLN
 EXTERN TOGINS0				;PART2
 EXTERN QSAVE1				;PART1
;
.LIST ON
;
FIELD_NAMES:	EQU $FEC0
;
OVERLAY_IN_MEMORY:
;1=DATABASE, 0=OUTLINER
 DB OVERLAY_ASSEMBLED ; 2019 Configured from WW.ASM, WWDATA.ASM
;
;IN_DATABASE_MODE:
;1=IN DB MODE, 0=NOT IN DB MODE
; DB 0
;
;OUTLINER CONTROL CHARACTERS
OUTMODE:
 DB 0           ;0 = OUTLINER INACTIVE, NZ = ACTIVE
;
;--------------------
;
FC_ENTER_OUTLINER: 
;
 LDA OUTMODE		;ARE WE IN OUTLINER NOW?
 BNE EXITOUT    	;IF SO, GO TRY TO LEAVE THE OUTLINER
;
 LDX IN_DATABASE_MODE	;ARE WE IN THE DATABASE NOW?
 BEQ ?OUTL_OR_CARDF	;NOT, ENTER DB OR OUTLN
 JMP EXIT_DATABASE_MODE	;GO TRY TO EXIT THE DATABASE
;
?OUTL_OR_CARDF:
 LDX #40        	;TRY TO ENTER OUTLINER.
 STX MERPTR		;FLAG "GOOD SAVE" FOR ENTRY
 JSR PRKECY     	;'ENTER OUTLINER OR CARDFILE?(O/C)'
 CMP #'C'		;WANTS THE CARDFILE?
 BEQ ?FORCE_YN       	;GO TO CARDFILE!
 CMP #'O'		;FALL THRU FOR OUTLINER
 BNE OUTLINE3		;OR EXIT
;
?FORCE_YN: 
 PHA		;SAVE (O) OR (C)
 LDX #44
 JSR PRKECY     ;OUTLINER ERASES MEMORY! SAVE FILE?(Y/N)
 BEQ ?DO_SAVE
 CMP #'N'
 BEQ OUTLINE0   ;NO SAVE
 BNE ?FORCE_YN
;
?DO_SAVE: 
 JSR QSAVE1     ;ENTER QUICK SAVE.
;
OUTLINE0:
;
 PLA
 CMP #'C'
 BEQ FC_MINI_DATABASE
;
 LDA OVERLAY_IN_MEMORY		;WHICH OVERLAY IS IN MEMORY NOW?
 BEQ ?OUTLINER_IN_MEMORY	;IF OUTLINER, GO ON
;
 LDA #'1'			;SELECT CARDFILE
 JSR LOAD_OVERLAY		;TRY TO LOAD IT
; BCS OUTLINE3			;WE DIDN'T GET THE FILE
 BCS FMD_ABORT
 DEC OVERLAY_IN_MEMORY		;[0]
;
?OUTLINER_IN_MEMORY:
 INC OUTMODE    ;SET OUTMODE = 1
 LDA #0
 STA INSERTM    ;SORRY, NO INSERT IN OUTLINER
 JSR TOGINS0    ;SET OVR IN HEADER
;
 SEC
 JSR DRAWOUT ;PLACE 'OUTLINER' IN HEADER
;
 JMP MNLINE
;
;--------------------
;
EXITOUT:
;*NOTE* THIS SHOULD MOVE INSIDE OUTLINER CODE
 LDX #43 		;'EXIT OUTLINE MODE?(Y/N)'
 JSR PRKECY
 BNE OUTLINE3
;
 CLC
 JSR DRAWOUT 		;PLACE '        ' IN HEADER     
 DEC OUTMODE
OUTLINE3:
 JMP ON_AND_REFREASH
;
;--------------------
;
FC_MINI_DATABASE:
;
?ENTER_DB:
;
 LDA OVERLAY_IN_MEMORY
 BNE ?ENTER_OVERLAY
;
 LDA #'2'
 JSR LOAD_OVERLAY
 BCS FMD_ABORT			;WE DIDN'T GET IT
 INC OVERLAY_IN_MEMORY		;[1] WE GOT IT
;
?ENTER_OVERLAY: 
 JMP ENTER_DATABASE_MODE 
FMD_ABORT:
 RTS
;
;-------------------- 
; 
LOAD_OVERLAY:
;.A NUMBER OF OVERLAY TO LOAD
 STA OVERLAY_NUMBER		;SELECT OVERLAY
;
 LDX #7
 JSR PRKECY			;'PLACE PROGRAM DISK IN DRIVE'
;
LOAD_OVERLAY_DIRECT:
 JSR LOAD_ANY_FILE		;LOAD THE SELECTED OVERLAY
 DB #>OVERLAY_FILENAME
 DB #USE_DATA_DISK
 DB 3
 DB #<OVERLAY_FILENAME
 DB #>OVERLAY_START
 DB #<OVERLAY_START
 RTS
;
;-------------------- 
; 
OVERLAY_FILENAME:
 DB 'OV'
OVERLAY_NUMBER:
 DB '1'
;
;-------------------- 
;
 END
;
