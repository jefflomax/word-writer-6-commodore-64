;
 TITLE FONT_WINDOW
;
 PW 92
 INCLUDE WWSETUP.ASM
 INCLUDE VARIABLS.ASM
;
 PUBLIC FC_FONT_WINDOW
;
 EXTERN CLR_BACKGROUND
 EXTERN CURRENT_CHARACTER_Y
 EXTERN AD28SM
 EXTERN AX_INTO_MSGOUT
 EXTERN BP_MIN_CUR_PAGE_LENGTH
 EXTERN BP_PLS_CUR_PAGE_LENGTH
 EXTERN BUMP_LINE_PAGE
 EXTERN CALCULATE_LINE_LENGTH1
 EXTERN CHKNUM
 EXTERN CLRBLK
 EXTERN DISPLY
 EXTERN EDIT_LINE
 EXTERN FC_LINE_OUT
 EXTERN FC_REFORMAT_FROM_CURSOR
 EXTERN FC_SHOW_STATUS
 EXTERN FC_WORK_KEY_INS
 EXTERN FONT_TABLE_X_INTO_PT
 EXTERN FULL_SCREEN
 EXTERN INC_PT_BY_1
 EXTERN KEYBRD
 EXTERN ON_AND_REFREASH
 EXTERN OUTMODE
 EXTERN PULLDN
 EXTERN RESET
 EXTERN START_LINE_FONT
 EXTERN SWAPXY
 EXTERN VID_A_TO_SCRMO
 EXTERN WRAP_LINE
 EXTERN W_INTO_MSGOUT			;POINTERM
;
FC_FONT_WINDOW:
;
 LDX OUTMODE
 BNE ?NO_FONTS
;
 LDX CURRENT_FONT
 BPL ?SKP0
?NO_FONTS:
 RTS
;
?SKP0: 
 JSR CURRENT_CHARACTER_Y
 CMP #CMARK
 BNE ?CONT
 JMP FC_SHOW_STATUS
;
?CONT:
 STX POINTER3         ;CURRENT_FONT
;
; LDA #<FONT_BOX
; LDX #>FONT_BOX
; JSR AX_INTO_MSGOUT
; JSR W_INTO_MSGOUT
; DW FONT_BOX
;
 LDA #12  ;CBOFFSET
 JSR PULLDN ;DRAW THE BOX
 DW FONT_BOX
;
?BIG_LOOP:
 LDA #12+1+40+40
 JSR VID_A_TO_SCRMO
;
 LDX #0
 STX POINTER2               ;COUNTER 1..9
;
?OLOOP:
 STX POINTER2+1             ;COUNTER 0..2..
 JSR FONT_TABLE_X_INTO_PT   ;POINTER = FONT_TABLE,X .Y = NAME
 JSR INC_PT_BY_1
 LDX #13 
?LOOP:
 LDA POINTER2
 CMP POINTER3            ;CURRENT_FONT
 BEQ ?NEED_80
 LDA #$00
 DB $2C
?NEED_80:
 LDA #$80       ;$80 OR 0
;
 ORA (POINTER),Y
 JSR SWAPXY
 STA (SCRMO),Y
 JSR SWAPXY
 DEY
 DEX
 CPX #1
 BNE ?LOOP
 INC POINTER2
;
 LDY #0
 LDA POINTER2
 CLC
 ADC #'0'
 CMP #':'
 BNE ?SKP
 LDA #'0'
?SKP:
 STA (SCRMO),Y
;
 JSR AD28SM
 LDX POINTER2+1 ;COUNTER
 INX
 INX
 CPX #20        ;NUMBER OF INSTALLED FONTS/2
 BCC ?OLOOP
;
?KEYS_IN:
 JSR KEYBRD
;
 PHA
 JSR CHKNUM 
 PLA
 BCS ?CURS_DOWN
 SBC #$30
 BPL ?ONE_NINE
 LDA #9
?ONE_NINE:
 STA POINTER3
 BPL ?FORCE_FONT
; 
?CURS_DOWN:
 CMP #$11
 BNE ?CURS_UP
 INC POINTER3 
 LDA POINTER3
 CMP #10
 BCC ?BIG_LOOP 
 LDA #0
?STORE_LOOP:
 STA POINTER3
 JMP ?BIG_LOOP
;
?CURS_UP:
 CMP #$91
 BNE ?RETURN
 DEC POINTER3
 LDA POINTER3
 BPL ?BIG_LOOP
?MAKE_END:
 LDA #$9
 BNE ?STORE_LOOP
;
?RETURN:
 CMP #$0D
 BNE ?KEYS_IN
;
?FORCE_FONT: 
 JSR RESET
;
 LDA POINTER3               ;NEW FONT
 LDX CURRENT_CHAR           ;DOES THIS UPDATE START_LINE?
 BNE ?NO_START_UPDATE
 STA START_LINE_FONT        ;NEW START LINE FONT
;
;
?NO_START_UPDATE:
 CMP #9   ;0..9 = 10
 BNE ?SKP1
 LDA #$FF 
?SKP1:
 CLC
 ADC #'1'+$80
 JSR FC_WORK_KEY_INS
;
 LDA EDIT_LINE
 CMP #CMARK
 BEQ ?SKP3
;
 LDA CURRENT_FONT
 PHA 
; 
 JSR BP_MIN_CUR_PAGE_LENGTH
;
 JSR CALCULATE_LINE_LENGTH1
 JSR BUMP_LINE_PAGE
;*MUST FIX*
; BRK
 LDA LINE
 CMP #0 
 BNE ?NO_LINE_CHANGE 
 INC LINE       ;THIS IS FONT MODE ONLY, SPECIAL COND, MAY BE OK
;*MUST FIX*
 JSR FC_LINE_OUT
;
?NO_LINE_CHANGE:
 PLA
 STA CURRENT_FONT 
 JSR CLR_BACKGROUND
;
;JSR DISPLY
;*NOTE* REMOVED DISPLAY 2/6/91
;*NOTE*
; FULL_SCREEN:
;
; LDA EDIT_LINE
; CMP #CMARK
; BEQ ?SKP3
 JSR FC_REFORMAT_FROM_CURSOR
;
?SKP3:
 JMP FULL_SCREEN
;
;
.IF REMOVE_TEXT
FONT_BOX
 DB 16
 DB 112,$FF,64,13,110
 DB 93,$FF,32,3,'FONTS',$FF,32,4,93
 DB 93,$FF,32,13,93
 DB 93,$FF,32,13,93
 DB 93,$FF,32,13,93
 DB 93,$FF,32,13,93
 DB 93,$FF,32,13,93
 DB 93,$FF,32,13,93
 DB 93,$FF,32,13,93
 DB 93,$FF,32,13,93
 DB 93,$FF,32,13,93
 DB 93,$FF,32,13,93
 DB 109,$FF,64,13,125,0
.ENDIF
;
;
FONT_BOX
 DB 15
 DB $FF,64,13
 DB $FF,32,3,'FONTS',$FF,32,4
 DB $FF,32,13
 DB $FF,32,13
 DB $FF,32,13
 DB $FF,32,13
 DB $FF,32,13
 DB $FF,32,13
 DB $FF,32,13
 DB $FF,32,13
 DB $FF,32,13
 DB $FF,32,13
 DB $FF,64,13,0
;
;
 END
;
